plugins {
  id 'maven-publish'
  id 'net.minecraftforge.gradle' version '[6.0.24,6.2)'
  id 'org.spongepowered.mixin' version '0.7.+'
  id 'com.matthewprenger.cursegradle' version '1.4.0'
  id 'nu.studer.credentials' version '3.0'
  id "com.modrinth.minotaur" version "2.+"
}

apply plugin: 'net.minecraftforge.gradle'

base {
  archivesName = "${project.mod_id}-forge-${project.minecraft_version}"
}

minecraft {
  mappings channel: 'official', version: "${project.minecraft_version}"

  reobf = false
  copyIdeResources = true

  accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

  runs {
    client {
      workingDirectory project.file('run/client')
      property 'forge.logging.markers', 'REGISTRIES'
      property 'forge.logging.console.level', 'debug'
      property 'forge.enabledGameTestNamespaces', "${project.mod_id}"

      mods {
        "${project.mod_id}" {
          source sourceSets.main
        }
      }

      // Mixin Support
      property 'mixin.env.remapRefMap', 'true'
      property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
    }

    client2 {
      parent minecraft.runs.client
      workingDirectory project.file('run/client2')
      args '--username', 'guest_player'
    }

    server {
      workingDirectory project.file('run/server')
      property 'forge.logging.markers', 'REGISTRIES'
      property 'forge.logging.console.level', 'debug'
      property 'forge.enabledGameTestNamespaces', "${project.mod_id}"

      mods {
        "${project.mod_id}" {
          source sourceSets.main
        }
      }

      // Mixin Support
      property 'mixin.env.remapRefMap', 'true'
      property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
    }

    gameTestServer {
      workingDirectory project.file('run/gametestserver')
      property 'forge.logging.markers', 'REGISTRIES'
      property 'forge.logging.console.level', 'debug'
      property 'forge.enabledGameTestNamespaces', "${project.mod_id}"

      mods {
        "${project.mod_id}" {
          source sourceSets.main
        }
      }

      // Mixin Support
      property 'mixin.env.remapRefMap', 'true'
      property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
    }

    data {
      workingDirectory project.file('run/data')
      property 'forge.logging.markers', 'REGISTRIES'
      property 'forge.logging.console.level', 'debug'
      property 'forge.enabledGameTestNamespaces', "${project.mod_id}"

      args '--mod', "${project.mod_id}", '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
      mods {
        "${project.mod_id}" {
          source sourceSets.main
        }
      }
    }
  }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
  minecraft "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"

  // Mixin dependencies
  annotationProcessor "org.spongepowered:mixin:0.8.7:processor"

  // Include Common code
  compileOnly project(":Common")

  // Test dependencies
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
  testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.1'

  // Additional helpful Mods for development (runtime dependency)
  if (project.hasProperty('forge_jei_mod') && project.forge_jei_mod) {
    runtimeOnly "curse.maven:jei-238222:${project.forge_jei_mod}"
  }
  if (project.hasProperty('forge_xaeros_minimap_mod') && project.forge_xaeros_minimap_mod) {
    runtimeOnly "curse.maven:xaeros_minimap-263420:${project.forge_xaeros_minimap_mod}"
  }

  // Hack fix for now, force jopt-simple to be exactly 5.0.4 because Mojang ships that version, but some transitive dependencies request 6.0+
  implementation('net.sf.jopt-simple:jopt-simple:5.0.4') { version { strictly '5.0.4' } }
}

mixin {
  config 'easy_npc.forge.mixins.json'

  add sourceSets.main, "easy_npc.refmap.json"
  debug.verbose = true
  debug.export = true
}

tasks.withType(JavaCompile).configureEach {
  source(project(":Common").sourceSets.main.allSource)
}

processResources {
  from project(":Common").sourceSets.main.resources
}

tasks.named('jar', Jar).configure {

  from(project(":Common").sourceSets.main.output)

  duplicatesStrategy = DuplicatesStrategy.EXCLUDE

  manifest {
    attributes([
        "Specification-Title"     : "${project.mod_name}",
        "Specification-Vendor"    : "${project.vendor_name}",
        "Specification-Version"   : "1",
        "Implementation-Title"    : project.name,
        "Implementation-Version"  : "${project.version}",
        "Implementation-Vendor"   : "${project.mod_id}",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        "MixinConfigs"            : "easy_npc.forge.mixins.json",
    ])
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId project.group
      artifactId project.archivesBaseName
      version project.version
      artifact jar
    }
  }
}

tasks.named('build') {
  finalizedBy tasks.named('publishToMavenLocal')
}

test {
  useJUnitPlatform()
  exclude '**/*'
}

// Merge the resources and classes into the same directory.
// This is done because java expects modules to be in a single directory.
// And if we have it in multiple we have to do performance intensive hacks like having the UnionFileSystem
// This will eventually be migrated to ForgeGradle so modders don't need to manually do it. But that is later.
sourceSets.each {
  def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
  it.output.resourcesDir = dir
  it.java.destinationDirectory = dir
}

// CurseForge Publishing
if (project.hasProperty('curseforge_project_id') && project.curseforge_project_id) {
  curseforge {
    apiKey = "${credentials.forKey('curseforge_api_token')}"
    project {
      id = "${project.curseforge_project_id}"
      changelog = rootProject.file("../CHANGELOG.md")
      changelogType = 'markdown'
      releaseType = "${project.curseforge_release_type}"
      addGameVersion("${project.minecraft_version}")
      addGameVersion("Forge")
      mainArtifact(jar)
    }
  }
}

// Modrinth Publishing
if (project.hasProperty('modrinth_project_id') && project.modrinth_project_id) {
  modrinth {
    token = "${credentials.forKey('modrinth_api_token')}"
    projectId = "${project.modrinth_project_id}"
    versionNumber = "${project.version}"
    versionType = "${project.modrinth_release_type}"
    versionName = "${project.mod_name} ${project.version}"
    changelog = rootProject.file("../CHANGELOG.md").text
    uploadFile = jar
    gameVersions = ["${project.minecraft_version}"]
    loaders = ["forge"]
  }
}
