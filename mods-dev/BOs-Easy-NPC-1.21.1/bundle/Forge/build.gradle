plugins {
  id 'maven-publish'
  id 'net.minecraftforge.gradle' version '[6.0,6.2)'
  id 'com.matthewprenger.cursegradle' version '1.4.0'
  id 'nu.studer.credentials' version '3.0'
  id "com.modrinth.minotaur" version "2.+"
}

apply plugin: 'net.minecraftforge.gradle'

base {
  archivesName = "${project.mod_id}-forge-${project.minecraft_version}"
}

minecraft {
  mappings channel: 'official', version: "${project.minecraft_version}"

  reobf = false
  copyIdeResources = true

  runs {
    client {
      workingDirectory project.file('run/client')
      property 'forge.logging.markers', 'REGISTRIES'
      property 'forge.logging.console.level', 'debug'
      property 'forge.enabledGameTestNamespaces', "${project.mod_id}"

      mods {
        "${project.mod_id}" {
          source sourceSets.main
        }
      }

      // Mixin Support
      property 'mixin.env.remapRefMap', 'true'
      property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
    }

    server {
      workingDirectory project.file('run/server')
      property 'forge.logging.markers', 'REGISTRIES'
      property 'forge.logging.console.level', 'debug'
      property 'forge.enabledGameTestNamespaces', "${project.mod_id}"

      mods {
        "${project.mod_id}" {
          source sourceSets.main
        }
      }
    }
  }
}

dependencies {
  minecraft "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"

  // Include Common code
  compileOnly project(":Common")

  // Regular dependencies for development mode (runClient/runServer)
  runtimeOnly(fg.deobf(group: 'de.markusbordihn.easynpc', name: "easy_npc-forge-${project.minecraft_version}", version: "${project.version}")) {
    changing = true
  }
  runtimeOnly(fg.deobf(group: 'de.markusbordihn.easynpc.configui', name: "easy_npc_config_ui-forge-${project.minecraft_version}", version: "${project.version}")) {
    changing = true
  }
}

tasks.named('compileJava', JavaCompile).configure {
  source(project(":Common").sourceSets.main.allSource)
}

tasks.named('compileTestJava', JavaCompile).configure {
  enabled = !sourceSets.test.allSource.isEmpty()
}

def getNextMajorVersion(String currentVersion) {
  def versionParts = currentVersion.tokenize('.')
  if (versionParts.size() > 0) {
    def majorVersion = versionParts[0].toInteger()
    return "${majorVersion + 1}.0.0"
  }
  return "1.0.0"
}

processResources {
  from project(":Common").sourceSets.main.resources
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE

  inputs.property 'version', project.version

  def nextMajor = getNextMajorVersion(project.version.toString())
  inputs.property 'nextMajorVersion', nextMajor

  filesMatching(['META-INF/mods.toml']) {
    expand 'file': [
        'jarVersion'      : project.version,
        'nextMajorVersion': nextMajor
    ]
  }
}

tasks.named('jar', Jar).configure {
  manifest {
    attributes([
        "Specification-Title"     : "${project.mod_name}",
        "Specification-Vendor"    : "${project.vendor_name}",
        "Specification-Version"   : "1",
        "Implementation-Title"    : project.name,
        "Implementation-Version"  : "${project.version}",
        "Implementation-Vendor"   : "${project.mod_id}",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
    ])
  }
}

// Merge the resources and classes into the same directory.
// This is done because java expects modules to be in a single directory.
// And if we have it in multiple we have to do performance intensive hacks like having the UnionFileSystem
// This will eventually be migrated to ForgeGradle so modders don't need to manually do it. But that is later.
sourceSets.each {
  def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
  it.output.resourcesDir = dir
  it.java.destinationDirectory = dir
}

// CurseForge Publishing
if (project.hasProperty('curseforge_project_id') && project.curseforge_project_id) {
  curseforge {
    apiKey = "${credentials.forKey('curseforge_api_token')}"
    project {
      id = "${project.curseforge_project_id}"
      changelog = rootProject.file('../CHANGELOG.md')
      changelogType = 'markdown'
      releaseType = "${project.curseforge_release_type}"
      addGameVersion("${project.minecraft_version}")
      addGameVersion("Forge")
      mainArtifact(jar) {
        relations {
          requiredDependency 'easy-npc-core'
          requiredDependency 'easy-npc-config-ui'
        }
      }
    }
  }
}

// Modrinth Publishing
if (project.hasProperty('modrinth_project_id') && project.modrinth_project_id) {
  modrinth {
    token = "${credentials.forKey('modrinth_api_token')}"
    projectId = "${project.modrinth_project_id}"
    versionNumber = "${project.version}"
    versionType = "${project.modrinth_release_type}"
    versionName = "${project.mod_name} ${project.version}"
    changelog = rootProject.file('../CHANGELOG.md').text
    uploadFile = tasks.jar
    gameVersions = ["${project.minecraft_version}"]
    loaders = ["forge"]
    dependencies {
      required.project "easy-npc-core"
      required.project "easy-npc-config-ui"
    }
  }
}
