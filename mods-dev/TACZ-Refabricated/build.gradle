plugins {
    id 'fabric-loom' version '1.11-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = "${archives_base_name}-${minecraft_version}"
}

fabricApi {
    configureDataGeneration {
        client = true
    }
}

loom {
    accessWidenerPath = file("src/main/resources/tacz.accesswidener")
    mixin {
        defaultRefmapName = "tacz.refmap.json"
    }
    runs {
        datagen {
            property "fabric-api.datagen.modid", "tacz"
        }
    }
}

repositories {
    maven {
        // 阿里云镜像，方便国内开发
        url = uri("https://maven.aliyun.com/repository/public/")
        content {
            includeGroup 'org.apache.commons'
        }
    }
    maven {
        // location of the maven that hosts JEI files since January 2023
        // Patchouli
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "Mod Maven"
        url = "https://modmaven.k-4u.nl"
    }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
    }
    maven { url "https://maven.shedaniel.me/" } // cloth config api
    maven { url "https://dvs1.progwml6.com/files/maven/" }
    maven { url "https://modmaven.dev" }
    maven {
        name = 'tterrag maven'
        url = 'https://maven.tterrag.com/'
    }
    maven {
        // Shedaniel's maven (Architectury API)
        url = "https://maven.architectury.dev"
        content {
            includeGroup "dev.architectury"
        }
    }
    maven {
        // latvian.dev Maven (KubeJS and Rhino)
        url "https://maven.latvian.dev/releases"
        content {
            includeGroup "dev.latvian.mods"
            includeGroup "dev.latvian.apps"
        }
    }
    maven {
        url 'https://jitpack.io'
        content {
            includeGroup "com.github.rtyley"
            includeGroup "com.github.FiguraMC.luaj"
        }
    }
    maven {
        name "KosmX's maven"
        url 'https://maven.kosmx.dev/'
    }
    maven {// CCA
        name = "Ladysnake Mods"
        url = 'https://maven.ladysnake.org/releases'
    }
    maven {// Forge Config API Port
        name = "Fuzs Mod Resources"
        url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
    }
    maven {// ModMenu
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
    maven {
        name = "devOSSnapshots"
        url = uri("https://mvn.devos.one/snapshots")
    }
    mavenCentral()
    mavenLocal()
    flatDir {
        dir 'libs'
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    //mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${project.parchment_minecraft_version}:${project.parchment_mappings_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modCompileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    annotationProcessor 'com.google.code.findbugs:jsr305:3.0.2'

    // Apache Commons Math 库，用于进行一些插值运算
    implementation(include('org.apache.commons:commons-math3:3.6.1'))

    // LuaJ 库，将 lua 脚本语言引入用于控制枪械的逻辑和动画状态机
    implementation(include('com.github.FiguraMC.luaj:luaj-core:3.0.8-figura')) {
        exclude group: 'org.apache.commons', module: 'commons-lang3'
    }
    implementation(include('com.github.FiguraMC.luaj:luaj-jse:3.0.8-figura')) {
        exclude group: 'org.apache.commons', module: 'commons-lang3'
    }
    implementation(include('org.apache.bcel:bcel:6.6.1')) {
        exclude group: 'org.apache.commons', module: 'commons-lang3'
    }

    // compile against the JEI API but do not include it at runtime - commented out for 1.21.1
    // modCompileOnly("mezz.jei:jei-1.20.1-common-api:${jei_version}")
    // modCompileOnly("mezz.jei:jei-1.20.1-fabric-api:${jei_version}")
    // at runtime, use the full JEI jar for Fabric
    // modRuntimeOnly("mezz.jei:jei-1.20.1-fabric:15.20.0.104")
    // 性能分析工具模组
    // modRuntimeOnly "curse.maven:spark-361579:4738953"
    // 假人，测试近战伤害
    // modRuntimeOnly "curse.maven:selene-499980:6902843"
    // modRuntimeOnly "curse.maven:mmmmmmmmmmmm-225738:6826546"
    // 手柄按键支持
    // modCompileOnly "curse.maven:framework-549225:6531429"
    // modCompileOnly("curse.maven:controllable-317269:6171420")

    // 越肩视角，用来兼容第三人称准星 - commented out for 1.21.1
    // modImplementation "curse.maven:shoulder-surfing-reloaded-243190:6784146"

    modImplementation "me.shedaniel.cloth:cloth-config-fabric:${cloth_config_fabric}"

    // modCompileOnly "curse.maven:embeddium-908741:${embeddedt_id}"
    // 同时兼容两个版本 - commented out for 1.21.1
    // modCompileOnly 'libs:iris_legacy:mc1.20.1-1.6.17'
    // modCompileOnly 'libs:iris:1.7.0+mc1.20.1'

    // modCompileOnly "curse.maven:carry-on-274259:${carry_on_id}"

    modImplementation "dev.kosmx.player-anim:player-animation-lib-fabric:${player_animation_lib}"

    // kubejs开发环境引入 - commented out due to unavailability for 1.21.1
    // modCompileOnly "dev.latvian.mods:kubejs-fabric:${kubejs_version}"
    // modCompileOnly "dev.latvian.mods:rhino-fabric:${rhino_version}"
    // modCompileOnly "dev.architectury:architectury-fabric:${architectury_version}"

    // CCA - using 1.21.1 compatible versions
    modImplementation(include("org.ladysnake.cardinal-components-api:cardinal-components-base:6.1.1"))
    modImplementation(include("org.ladysnake.cardinal-components-api:cardinal-components-entity:6.1.1"))

    // MKB - using 1.21 compatible version
    modImplementation(include("maven.modrinth:modernkeybinding:1.21.X-1.3.0"))

    // Forge Config API Port - using 1.21.1 compatible version
    modApi "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:21.1.1"

    // ModMenu - using 1.21.1 compatible version
    modImplementation("com.terraformersmc:modmenu:11.0.3")

    // Zoomify - commented out for 1.21.1
    // modCompileOnly "maven.modrinth:zoomify:2.14.5+1.20.1"

    // modCompileOnly "maven.modrinth:immediatelyfast:1.5.1+1.20.4-fabric"

    // modRuntimeOnly "maven.modrinth:tweakeroo:0.17.1"
    // modRuntimeOnly "maven.modrinth:malilib:0.16.3"

    // REI - commented out for 1.21.1
    // modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:$rei_version"
    // modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:$rei_version"
    // modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:$rei_version"

    // 加速渲染 - commented out for 1.21.1
    // modImplementation "maven.modrinth:accrelatedrendering-refabricated:1.0.8-1.20.1-alpha-fabric.1"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = "${project.archives_base_name}-${project.minecraft_version}"
            from components.java
        }
    }

    repositories {
        maven {
            url "file:///D:/github_maven/Sh1roCu_MCRepo/repository"
        }
    }
}
